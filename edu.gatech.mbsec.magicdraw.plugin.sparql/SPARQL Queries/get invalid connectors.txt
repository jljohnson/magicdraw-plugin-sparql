PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX sysml: <http://omg.org/sysml/rdf#> 
PREFIX sysml_namedelement: <http://omg.org/sysml/rdf#NamedElement/> 
PREFIX sysml_block: <http://omg.org/sysml/rdf#Block/> 
PREFIX sysml_property: <http://omg.org/sysml/rdf#Property/> 
PREFIX sysml_connector: <http://omg.org/sysml/rdf#Connector/> 
PREFIX sysml_connectorend: <http://omg.org/sysml/rdf#ConnectorEnd/> 
SELECT DISTINCT ?connector   			
WHERE {
	?connector  rdf:type sysml:Connector . 
	?connector  sysml_connector:end ?connectorEnd1 . 
	?connectorEnd1  sysml_connectorend:role ?connectorEnd1Role .
	?connectorEnd1Role  sysml_property:type ?connectorEnd1Type . 
	?connector  sysml_connector:end ?connectorEnd2 . 
	?connectorEnd2  sysml_connectorend:role ?connectorEnd2Role . 
	?connectorEnd2Role  sysml_property:type ?connectorEnd2Type . 					
	FILTER (?connectorEnd1Type != ?connectorEnd2Type) 					
	FILTER (NOT EXISTS { ?connectorEnd1Type sysml_block:inheritedBlock+ ?connectorEnd2Type }) 	
	FILTER (NOT EXISTS { ?connectorEnd2Type sysml_block:inheritedBlock+ ?connectorEnd1Type }) 				
}